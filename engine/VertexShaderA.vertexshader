#version 330 core
// Suggests drivers to unroll loops.
#pragma optionNV(unroll all)
#pragma unroll

layout(location = 0) in vec3 vertexPosition_model;
layout(location = 1) in vec4 vertexColor;
layout(location = 2) in vec3 vertexNormal_model;

uniform mat4 model_transform;
uniform mat4 camera_transform;
uniform mat4 perspective_transform;

uniform int anim_mode = 0;
uniform float anim_phase;
uniform int light_number = 1;
uniform vec3 lightpos_global[12];

out vec4 baseColor;
out vec3 normal_cameraspace;
out vec3 vector_to_light_cameraspace[12];
out vec3 vector_to_camera_cameraspace;
out float lightdistance[12];
out vec3 fragmentPosition_cameraspace;

void main(){
    vec4 color = vertexColor;

    vec4 vertexPosition_cameraspace = camera_transform * model_transform * vec4(vertexPosition_model,1.0f);
    vec4 vertexPosition_perspective =  perspective_transform * vertexPosition_cameraspace;

    vec3 camerapos_cameraspace = vec3(0.0,0.0,0.0);
    vector_to_camera_cameraspace = camerapos_cameraspace - vertexPosition_cameraspace.xyz;
    fragmentPosition_cameraspace = vertexPosition_cameraspace.xyz;

    for(int i = 0; i < 12; i++){ // Will be unrolled!
        if(i >= light_number) break;
        vec3 lightpos_cameraspace = ( camera_transform * vec4(lightpos_global[i],1.0f)).xyz;
        lightdistance[i] = distance(vertexPosition_cameraspace.xyz, lightpos_cameraspace);
        vector_to_light_cameraspace[i] = lightpos_cameraspace + vector_to_camera_cameraspace;
    }

    normal_cameraspace = normalize( camera_transform * model_transform * vec4(vertexNormal_model,0.0f)).xyz;

    gl_Position = vertexPosition_perspective;

    float anim_alpha = 1.0;
    if(anim_mode == 1) anim_alpha = 1.0 - anim_phase; // fadeout
    color.a *= anim_alpha;

    baseColor = color;

}
